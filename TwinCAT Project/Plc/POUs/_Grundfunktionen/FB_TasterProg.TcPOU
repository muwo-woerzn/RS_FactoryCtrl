<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4018.5">
  <POU Name="FB_TasterProg" Id="{fd9119ea-530b-4713-b29f-7eca2d52672f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TasterProg
VAR_INPUT
	bCfgRollo 	: BOOL;
	bIn 		: BOOL;
END_VAR

VAR_OUTPUT
	bKlick			: BOOL;
	bKlick2			: BOOL;
	bKlick3			: BOOL;
	bLang				: BOOL;
	bProg				: BOOL;
	iKlick			: INT;
	iProgCode		: INT;
END_VAR
VAR
	bKlick_i		: BOOL;

	iProgState	: INT;
	iKlickCnt		: INT;
	iCode1			: INT;
	iCode10			: INT;

	tofPrell 		: TOF := (PT:= t#30ms);
	tofKlick 		: TOF := (PT:= t#200ms);
	tonLang 		: TON := (PT:= t#500ms);
	tonProg 		: TON := (PT:= t#3s);
	tofProg	 		: TOF := (PT:= t#5s);

	rtrigTast	 	: R_TRIG;
	ftrigKlick 	: F_TRIG;
	rtrigLang  	: R_TRIG;

END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[tofPrell(IN:= bIn);
rtrigTast(CLK:= tofPrell.Q);
bKlick_i := rtrigTast.Q AND NOT tofKlick.Q;

tofKlick(IN:= rtrigTast.Q);
ftrigKlick(CLK:= tofKLick.Q);

IF rtrigTast.Q THEN
	iKlickCnt := iKlickCnt + 1;
END_IF

CASE iProgState OF
0: (* normal: *)
	tonLang(IN:= tofPrell.Q);
	rtrigLang(CLK:= tonLang.Q);

	tonProg(IN:= tofPrell.Q);
	IF tonProg.Q THEN
		tofProg(IN:= FALSE);
		tofProg(IN:= TRUE);
		iProgState := iProgState + 1;
		iKlickCnt := 0;
	END_IF

1: (* Progstatus, Codewert x10 *)
	IF ftrigKlick.Q THEN
		IF iKlickCnt = 0 THEN
			iProgState := 0;
		ELSE
			iCode10 := iKlickCnt;
			iKlickCnt := 0;
			iProgState := iProgState + 1;
		END_IF
	END_IF

2: (* Progstatus, Codewert x1 *)
	IF ftrigKlick.Q THEN
		IF iKlickCnt = 0 THEN
			iProgState := 0;
		ELSE
			iCode1 := iKlickCnt;
			iKlickCnt := 0;
			iProgState := iProgState + 1;
		END_IF
	END_IF

3: (* Codewertausgabe *)
	iProgCode := iCode10 * 10 + iCode1;
	iProgState := iProgState + 1;

4:
	iProgCode := 0;
	iProgState := 0;

END_CASE


(* Ausgabe *)
CASE iProgState OF
0:
	bProg := FALSE;
	iProgCode	:= 0;

	bKlick := bKlick_i;
	IF ftrigKlick.Q THEN
		iKlick := iKlickCnt;
		bKlick2 := iKlick = 2;
		bKlick3 := iKlick = 3;
		iKlickCnt := 0;
	ELSE
		iKlick := 0;
		bKlick2 := FALSE;
		bKlick3 := FALSE;
	END_IF

	IF bCfgRollo THEN
		bLang := tonLang.Q;
	ELSE
		bLang := rtrigLang.Q;
	END_IF


ELSE
	bKlick := FALSE;
	bKlick2 := FALSE;
	bKlick3 := FALSE;
	bLang := FALSE;
	bProg := TRUE;
	iKlick := 0;

	tofProg(IN:= rtrigTast.Q);
	IF NOT tofProg.Q THEN
		iProgState := 0;
	END_IF

END_CASE
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>