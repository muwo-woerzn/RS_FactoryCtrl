<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4018.9">
  <POU Name="FB_RolloGruppe" Id="{5e030dd8-760d-4ec3-a668-4f17f48a934d}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_RolloGruppe
VAR_INPUT
	bCfgTimeAb				: BOOL; (* Rollo ab, bei globaler Schaltzeit *)
	bCfgTimeAbWinter	: BOOL; (* Rollo ab, bei globaler Schaltzeit, aber nur waehrend Winderzeit *)
	iAnzRollo					: INT := 1;
	aitGesamt 				: ARRAY[1..3] OF INT;
	aitSchatten 			: ARRAY[1..3] OF INT;
END_VAR
VAR_OUTPUT

END_VAR
VAR
	bInit 			: BOOL := TRUE;
	bInVisuAuf 	: BOOL;
	bInVisuAb		: BOOL;
	bInAuf 			: BOOL;
	bInAb				: BOOL;

	iArr				: INT;

	(* %I / %Q ********************)
	IbInTasterAuf		AT %I* : BOOL;
	IbInTasterAb	 	AT %I* : BOOL;
	QbOutRolloAb		AT %Q* : BOOL;
	QbOutRollo1	 		AT %Q* : BOOL;
	QbOutRollo2	 		AT %Q* : BOOL;
	QbOutRollo3	 		AT %Q* : BOOL;

	(* Rollosteuerung *)
	bAbVorw 		: BOOL;
	bAb					: BOOL;

	fbRollo 		: ARRAY[1..3] OF FB_Rollo;
	tonRolloAb	: TON := (PT:= t#30s);

  fbLog : FB_LoggerSetMsg;
	bLogMsgAuf : BOOL;
	bLogMsgAb : BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bInit THEN
	bInit := FALSE;
	FOR iArr := 1 TO 3 DO
		fbRollo[iArr].bCfgTimeAb := bCfgTimeAb;
		IF (iArr = 3) THEN (* soll nur fuer 3. Rollo im Esszimmer gelten = Tuere *)
			fbRollo[iArr].bCfgTimeAbWinter := bCfgTimeAbWinter;
		END_IF
		fbRollo[iArr].itGesamt := aitGesamt[iArr];
		fbRollo[iArr].itSchatten := aitSchatten[iArr];
	END_FOR
END_IF

bInAuf := IbInTasterAuf OR bInVisuAuf;
bInAb := IbInTasterAb OR bInVisuAb;

fbRollo[1](
	bInTasterAuf:= bInAuf,
	bInTasterAb:= bInAb,
	bAbVorw:= bAbVorw,
	bAb:= bAb,
	bOutRollo=> QbOutRollo1);

IF iAnzRollo >=2 THEN
	fbRollo[2](
		bInTasterAuf:= bInAuf,
		bInTasterAb:= bInAb,
		bAbVorw:= bAbVorw,
		bAb:= bAb,
		bOutRollo=> QbOutRollo2);
END_IF

IF iAnzRollo >=3 THEN
	fbRollo[3](
		bInTasterAuf:= bInAuf,
		bInTasterAb:= bInAb,
		bAbVorw:= bAbVorw,
		bAb:= bAb,
		bOutRollo=> QbOutRollo3);
END_IF

QbOutRolloAb := bAb;

tonRolloAb(IN:= QbOutRolloAb AND NOT (QbOutRollo1 OR QbOutRollo2 OR QbOutRollo3));
IF tonRolloAb.Q THEN
	bAbVorw := FALSE;
	bAb := FALSE;
END_IF

// Log Meldungen
IF NOT bLogMsgAuf AND bInAuf AND NOT QbOutRolloAb
AND (QbOutRollo1 OR QbOutRollo2 OR QbOutRollo3)
THEN
  fbLog.M_AddMsgHint('Rollo AUF Taster');
  bLogMsgAuf := TRUE;
ELSE
  bLogMsgAuf := FALSE;
END_IF
IF NOT bLogMsgAb AND bInAb AND QbOutRolloAb
AND (QbOutRollo1 OR QbOutRollo2 OR QbOutRollo3)
THEN
  fbLog.M_AddMsgHint('Rollo AB Taster');
  bLogMsgAb := TRUE;
ELSE
  bLogMsgAb := FALSE;
END_IF
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>